# Process this file with autoconf to produce a configure script.

# Say what we are:
AC_INIT([scponly], [3.0], [joe@sublimation.org])
AC_CONFIG_SRCDIR([helper.c])
AC_CONFIG_HEADERS([config.h])

AC_CANONICAL_HOST

if test "$host_os" = Solaris; then
  AC_DEFINE(SOLARIS_COMPAT)
fi


AC_DEFINE(HOST_OS,$host_os)

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PATH_PROG(PROG_CUT, cut)
AC_PATH_PROG(PROG_GREP, grep)
AC_PATH_PROG(PROG_SORT, sort)
AC_PATH_PROG(PROG_LDD, ldd)
AC_PATH_PROG(PROG_USERADD, useradd)
AC_PATH_PROG(PROG_PWD_MKDB, pwd_mkdb)

# Features wanted for this installation:
AC_ARG_ENABLE([wildcards-enabled],
	[  --disable-wildcards	disable wildcard processing], ,
	[AC_DEFINE([ENABLE_WILDCARDS])])

AC_ARG_ENABLE([winscp-compat],
	[  --disable-winscp-compat	disable WinSCP compatibility mode], ,
	[AC_DEFINE([WINSCP_COMPAT])])

AC_ARG_ENABLE([gftp-enabled],
	[  --disable-gftp-compat    disable gftp compatibility], ,
	[AC_DEFINE([GFTP_COMPAT])])

AC_ARG_ENABLE([sftp-enabled],
	[  --disable-sftp    disable sftp compatibility], ,
	[AC_DEFINE([ENABLE_SFTP])])

AC_ARG_ENABLE([scp-enabled],
	[  --disable-scp     disable scp compatibility], ,
	[AC_DEFINE([ENABLE_SCP])])

AC_ARG_ENABLE([chrooted-binary],
	[  --enable-chrooted-binary	install chrooted binary 'scponlyc'], 
	[AC_SUBST([CHROOTED_NAME], [scponlyc])
	 AC_DEFINE([CHROOTED_NAME], ["scponlyc"]) ])

# Check for binaries which will be launched by scponly
SCPONLY_PATH_PROG_DEFINE([PROG_SFTP_SERVER], [sftp-server],
                         [/usr/lib:/usr/libexec/openssh])
			 # Debian uses /usr/lib
			 # Red Hat uses /usr/libexec/openssh
			 # Many a *BSD uses $PATH itself
SCPONLY_PATH_PROG_DEFINE([PROG_LS],    [ls])
SCPONLY_PATH_PROG_DEFINE([PROG_SCP],   [scp])
SCPONLY_PATH_PROG_DEFINE([PROG_RM],    [rm])
SCPONLY_PATH_PROG_DEFINE([PROG_LN],    [ln])
SCPONLY_PATH_PROG_DEFINE([PROG_MV],    [mv])
SCPONLY_PATH_PROG_DEFINE([PROG_CHMOD], [chmod])
SCPONLY_PATH_PROG_DEFINE([PROG_CHOWN], [chown])
SCPONLY_PATH_PROG_DEFINE([PROG_MKDIR], [mkdir])
SCPONLY_PATH_PROG_DEFINE([PROG_RMDIR], [rmdir])

# Check for binaries required by the WinSCP compatibility mode
SCPONLY_PATH_PROG_DEFINE([PROG_PWD],    [pwd])
SCPONLY_PATH_PROG_DEFINE([PROG_GROUPS], [groups])
SCPONLY_PATH_PROG_DEFINE([PROG_ECHO],   [echo])

# Checks for libraries.
# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([stdlib.h string.h syslog.h unistd.h wordexp.h glob.h libgen.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

# Checks for library functions.
AC_FUNC_MALLOC
AC_CHECK_FUNCS([atexit bzero strchr strerror glob wordexp strspn basename])

AC_CONFIG_FILES([Makefile])
AC_OUTPUT([setup_chroot.sh])
